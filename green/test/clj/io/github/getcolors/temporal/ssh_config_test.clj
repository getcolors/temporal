(ns io.github.getcolors.temporal.ssh-config-test
  "Conformance with the workspace SSH Config Standard.

  Every test that needs a config file redirects `config-path` into a temporary
  directory: nothing here may read or write the real `~/.ssh/config`."
  (:require [babashka.fs :as fs]
            [clojure.java.io :as io]
            [clojure.string :as str]
            [clojure.test :refer [deftest is]]
            [green.scaffold :as sc]
            [io.github.getcolors.temporal.ssh-config :as ssh-config]
            [io.github.getcolors.temporal.tools :as tools]
            [io.github.getcolors.temporal.validate-test :refer [fixture keygen]]
            [io.github.getcolors.temporal.workflow :as workflow]))

(defn- with-config
  "Run `f` with `~/.ssh/config` redirected to a fresh temporary file holding
  `content` (or absent when nil)."
  [content f]
  (let [home (str (fs/create-temp-dir {:prefix "temporal-ssh-config"}))
        file (io/file home ".ssh" "config")]
    (try
      (when content
        (io/make-parents file)
        (spit file content))
      (with-redefs [ssh-config/config-path (constantly file)] (f file))
      (finally (fs/delete-tree home)))))

;; §2 the alias and the identity file

(deftest alias-is-the-profile
  (is (= "temporal-fixture" (ssh-config/host-alias (fixture)))))

(deftest identity-file-keeps-the-tilde
  ;; An expanded home directory would make the rendered block differ per
  ;; workstation; OpenSSH expands the tilde itself.
  (is (= "~/.ssh/temporal-fixture" (ssh-config/identity-file (fixture))))
  (is (not (str/includes? (ssh-config/identity-file (fixture))
                          (System/getProperty "user.home")))))

(deftest the-marker-is-the-alias-alone
  ;; The profile is <package>-<suffix>, so a marker carrying the package name
  ;; too would repeat it: "# BEGIN temporal temporal-digitalocean".
  (is (= "# BEGIN temporal-digitalocean ANSIBLE MANAGED BLOCK"
         (ssh-config/begin-marker "temporal-digitalocean")))
  (is (= "# END temporal-digitalocean ANSIBLE MANAGED BLOCK"
         (ssh-config/end-marker "temporal-digitalocean"))))

(deftest owned-markers-hold-the-one-marker
  ;; Born conforming: no marker migration is in flight, so the set of markers
  ;; this package recognises as its own holds exactly the current one.
  (is (= {:begin #{"# BEGIN temporal-digitalocean ANSIBLE MANAGED BLOCK"}
          :end #{"# END temporal-digitalocean ANSIBLE MANAGED BLOCK"}}
         (ssh-config/owned-markers "temporal-digitalocean"))))

;; §5 never adopt

(deftest host-patterns-are-read-from-a-host-line
  (is (= ["temporal-fixture"] (ssh-config/host-patterns "Host temporal-fixture")))
  (is (= ["web" "temporal-fixture" "db"] (ssh-config/host-patterns "  host   web temporal-fixture  db ")))
  (is (nil? (ssh-config/host-patterns "    HostName 192.0.2.1")))
  (is (nil? (ssh-config/host-patterns "Match host temporal-fixture"))))

(deftest a-foreign-stanza-is-found
  (let [lines ["Host other" "    HostName 192.0.2.1" "" "Host temporal-fixture"]]
    (is (= 4 (ssh-config/foreign-stanza-line lines "temporal-fixture")))))

(deftest our-own-block-is-not-foreign
  (let [alias "temporal-fixture"
        lines [(ssh-config/begin-marker alias)
               (str "Host " alias)
               "    HostName 192.0.2.1"
               (ssh-config/end-marker alias)]]
    (is (nil? (ssh-config/foreign-stanza-line lines alias)))))

(deftest a-stanza-after-our-block-is-still-foreign
  (let [alias "temporal-fixture"
        lines [(ssh-config/begin-marker alias)
               (str "Host " alias)
               (ssh-config/end-marker alias)
               (str "Host " alias)]]
    (is (= 4 (ssh-config/foreign-stanza-line lines alias)))))

(deftest a-block-under-a-package-prefixed-marker-is-foreign
  ;; This package never wrote a `# BEGIN temporal <alias>` marker, so a block
  ;; carrying one belongs to nobody this package knows and must stop the run
  ;; rather than being silently overwritten. Recognising a marker means
  ;; putting it in owned-markers at the same time.
  (let [alias "temporal-digitalocean"
        lines [(str "# BEGIN temporal " alias " ANSIBLE MANAGED BLOCK")
               (str "Host " alias)
               (str "# END temporal " alias " ANSIBLE MANAGED BLOCK")]]
    (is (= 2 (ssh-config/foreign-stanza-line lines alias)))))

(deftest a-multi-pattern-host-line-counts
  (is (= 1 (ssh-config/foreign-stanza-line ["Host web temporal-fixture db"]
                                           "temporal-fixture"))))

(deftest an-unrelated-file-is-left-alone
  (is (nil? (ssh-config/foreign-stanza-line ["Host build" "Host temporal-other"]
                                            "temporal-fixture"))))

(deftest adopt-error-names-the-file-and-the-line
  (with-config "Host other\n    HostName 192.0.2.1\n\nHost temporal-fixture\n    User root\n"
    (fn [file]
      (let [err (ssh-config/adopt-error (fixture))]
        (is (str/includes? err (.getPath file)))
        (is (str/includes? err "`Host temporal-fixture` at line 4"))
        (is (str/includes? err "will not overwrite it"))))))

(deftest adopt-error-passes-our-own-block-and-a-missing-file
  (with-config (str (ssh-config/begin-marker "temporal-fixture") "\n"
                    "Host temporal-fixture\n    HostName 192.0.2.1\n"
                    (ssh-config/end-marker "temporal-fixture") "\n")
    (fn [_] (is (nil? (ssh-config/adopt-error (fixture))))))
  (with-config nil
    (fn [_] (is (nil? (ssh-config/adopt-error (fixture)))))))

(deftest preflight-refuses-rather-than-overwrites
  (with-redefs [ssh-config/adopt-error (fn [_] "already declares `Host x`")
                ssh-config/placement-error (fn [_] nil)]
    (let [r (ssh-config/preflight! (fixture))]
      (is (= 1 (:green/exit r)))
      (is (str/includes? (:green/err r) "already declares")))))

(deftest preflight-passes-a-clean-file
  (with-redefs [ssh-config/adopt-error (fn [_] nil)
                ssh-config/placement-error (fn [_] nil)]
    (is (nil? (:green/exit (ssh-config/preflight! (fixture)))))))

(deftest preflight-reads-the-redirected-file
  ;; End to end through the real readers: a foreign stanza refuses, a clean
  ;; file passes, and the placement check runs after the ownership check.
  (with-config "Host temporal-fixture\n    HostName 192.0.2.1\n"
    (fn [_]
      (let [r (ssh-config/preflight! (fixture))]
        (is (= 1 (:green/exit r)))
        (is (str/includes? (:green/err r) "already declares")))))
  (with-config "ServerAliveInterval 60\nHost a\n"
    (fn [_]
      (let [r (ssh-config/preflight! (fixture))]
        (is (= 1 (:green/exit r)))
        (is (str/includes? (:green/err r) "line 1")))))
  (with-config "Host a\n    User root\n"
    (fn [_] (is (nil? (:green/exit (ssh-config/preflight! (fixture))))))))

;; §5 placement. The block is written with insertbefore: BOF, because
;; blockinfile anchors insertbefore on the *last* match and has no firstmatch.

(deftest an-option-above-the-first-host-is-refused
  ;; It is global today; a BOF insert would capture it into one stanza.
  (is (= 1 (ssh-config/leading-option-line ["ServerAliveInterval 60" "Host a"])))
  (is (= 3 (ssh-config/leading-option-line ["# comment" "" "IdentitiesOnly yes" "Host a"]))))

(deftest a-file-that-opens-with-a-host-is-fine
  (is (nil? (ssh-config/leading-option-line ["Host a" "    User root"])))
  (is (nil? (ssh-config/leading-option-line ["# lead comment" "" "Host a" "    User root"])))
  (is (nil? (ssh-config/leading-option-line ["Match host b" "    User root"]))))

(deftest a-file-of-only-comments-is-fine
  (is (nil? (ssh-config/leading-option-line ["# nothing here" ""]))))

(deftest placement-error-mentions-the-recovery
  (with-config "# comment\n\n\nIdentitiesOnly yes\nHost a\n"
    (fn [file]
      (let [err (ssh-config/placement-error (fixture))]
        (is (str/includes? err (.getPath file)))
        (is (str/includes? err "line 4"))
        (is (str/includes? err "Host *"))))))

;; §6 build determinism

(deftest build-and-dry-run-never-read-the-config
  ;; The only readers are adopt-error and placement-error, and they must not
  ;; run on a rendered-only event. Redefining them to throw proves nothing in
  ;; the build path calls them.
  (with-redefs [ssh-config/adopt-error (fn [_] (throw (ex-info "read ~/.ssh/config" {})))
                ssh-config/placement-error (fn [_] (throw (ex-info "read ~/.ssh/config" {})))]
    (doseq [opts [(assoc (fixture) :green/event :build)
                  (assoc (keygen) :green/event :build)
                  (assoc (fixture) :green/event :create :green/dry-run true)]]
      (is (= 0 (:green/exit (workflow/start-step opts {})))))))

(deftest the-local-play-renders-no-address
  ;; Address, user and alias are run-time facts and travel as extra-vars, so
  ;; the rendered playbook carries none of them.
  (let [data (tools/ansible-local-data (assoc (fixture) :ip "203.0.113.7"))]
    (is (not (contains? data :ip-rendered)))
    (is (= "~/.ssh/temporal-fixture" (:ssh-config-identity-file data)))))

(deftest the-local-stage-renders-three-files
  (let [targets (map #(str (:target %)) (tools/ansible-local-specs (fixture)))]
    (is (some #(str/ends-with? % "/ansible.cfg") targets))
    (is (some #(str/ends-with? % "/inventory.ini") targets))
    (is (some #(str/ends-with? % "/main.yml") targets))
    (is (every? #(str/includes? % "temporal-ansible-local") targets))))

;; §3 the identity file follows keygen mode

(deftest keygen-mode-decides-the-identity-lines
  (is (true? (:ssh-keygen (tools/ansible-local-data (keygen)))))
  (is (false? (:ssh-keygen (tools/ansible-local-data (fixture))))))

(defn- render-play [opts]
  (sc/render-template (tools/template "ansible-local" "main.yml")
                      (tools/ansible-local-data opts)
                      tools/template-opts))

(deftest local-updater-uses-managed-identity-only
 (is (str/includes? (render-play (keygen)) "colors_keygen: true"))
 (is (str/includes? (render-play (fixture)) "colors_keygen: false"))
 (is (str/includes? (render-play (fixture)) "fcntl.flock")))

(deftest create-writes-the-block-after-compute-and-before-convergence
  (is (= [:temporal/ssh-config]
         (vec (rest (workflow/wire-fn :temporal/infrastructure {:green/event :create})))))
  (is (= [:temporal/dns]
         (vec (rest (workflow/wire-fn :temporal/ssh-config {:green/event :create}))))))

(deftest delete-removes-the-block-before-the-destroy
  ;; The opposite of the keypair, which goes last. A stale block is harmless; a
  ;; key removed early locks the operator out of a machine that still exists.
  (is (= [:temporal/ssh-config]
         (vec (rest (workflow/wire-fn :temporal/dns {:green/event :delete})))))
  (is (= [:temporal/infrastructure]
         (vec (rest (workflow/wire-fn :temporal/ssh-config {:green/event :delete})))))
  (is (= []
         (vec (rest (workflow/wire-fn :temporal/infrastructure {:green/event :delete}))))))
